plugins {
  id 'maven-publish'
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'
}

configure(subprojects) {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  compileJava {
    options.encoding = 'UTF-8'
  }
  compileTestJava {
    options.encoding = 'UTF-8'
  }
}

subprojects {
  ext.appName = 'battlejar-client-root'

  repositories {
    mavenCentral()
    mavenLocal()
  }

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(25)
    }
  }

  afterEvaluate {
    version = rootProject.version
    tasks.named('jar', Jar) {
      archiveFileName.set("battlejar-${project.name}-${project.version}.jar")
    }
    publishing {
      publications {
        maven(MavenPublication) {
          from components.java
          groupId = rootProject.group
          artifactId = "battlejar-${project.name}"
          version = project.version
        }
        gpr(MavenPublication) {
          from(components.java)
          groupId = rootProject.group
          artifactId = "battlejar-${project.name}"
          version = project.version
        }
      }
      repositories {
        mavenLocal()
        maven {
          name = "GitHubPackages"
          url = uri("https://maven.pkg.github.com/mateusz-dykszak/battlejar-client")
          credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
          }
        }
      }
    }
  }
}

group = 'it.battlejar'
version = '0.1.0'
